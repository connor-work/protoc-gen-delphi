/// Copyright 2025 Connor Erdmann (connor.work)
/// Copyright 2020 Julien Scholz
/// Copyright 2020 Sotax AG
/// 
/// Licensed under the Apache License, Version 2.0 (the "License");
/// you may not use this file except in compliance with the License.
/// You may obtain a copy of the License at
/// 
///     http://www.apache.org/licenses/LICENSE-2.0
/// 
/// Unless required by applicable law or agreed to in writing, software
/// distributed under the License is distributed on an "AS IS" BASIS,
/// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
/// See the License for the specific language governing permissions and
/// limitations under the License.

/// <summary>
/// Support code for handling protobuf tags.
/// </summary>
unit Work.Connor.Protobuf.Delphi.ProtocGenDelphi.Runtime.Internal.uProtobufTag;

{$INCLUDE Work.Connor.Delphi.CompilerFeatures.inc}

{$IFDEF FPC}
  {$MODE DELPHI}
{$ENDIF}

interface

uses
  // TProtobufFieldNumber
  Work.Connor.Protobuf.Delphi.ProtocGenDelphi.uProtobuf,
  // TStream for encoding and decoding of tags
{$IFDEF WORK_CONNOR_DELPHI_COMPILER_UNIT_SCOPE_NAMES}
  System.Classes;
{$ELSE}
  Classes;
{$ENDIF}

type
  // Enum of all Protobuf wire types. See: https://developers.google.com/protocol-buffers/docs/encoding#structure.
  TProtobufWireType = (
    wtUnknown = -1,
    wtVarint = 0,
    wt64Bit = 1,
    wtLengthDelimited = 2,
    wtStartGroup = 3,
    wtEndGroup = 4,
    wt32Bit = 5
  );

  // Record type of a Protobuf tag.
  // Simply combines a field number and a wire type for convenience.
  // See: https://developers.google.com/protocol-buffers/docs/encoding#structure.
  TProtobufTag = record
    private
      FFieldNumber: TProtobufFieldNumber;
      FWireType: TProtobufWireType;
    public
      // Constructs a tag using a field number and a wire type.
      class function WithData(aFieldNumber: TProtobufFieldNumber; aWireType: TProtobufWireType): TProtobufTag; static;

      // Encodes the Protobuf tag according to the specification.
      // This is done by combining the field number and wire type with bitwise operations,
      // and then writing the result as a varint.
      // See: https://developers.google.com/protocol-buffers/docs/encoding#structure.
      // params:
      //   aDest: Stream to which binary data is appended.
      procedure Encode(aDest: TStream);

      // Decodes the Protobuf tag according to the specification from a binary stream.
      // This is done by reading a varint and then extracting the field number and wire
      // type using bitwise operations.
      // See: https://developers.google.com/protocol-buffers/docs/encoding#structure.
      // params:
      //   aSource: Stream from which the binary data should be read.
      procedure Decode(aSource: TStream);

      property FieldNumber: TProtobufFieldNumber read FFieldNumber;
      property WireType: TProtobufWireType read FWireType;
  end;

implementation

uses
  // For encoding/decoding of the tag's varint
  Work.Connor.Protobuf.Delphi.ProtocGenDelphi.Runtime.Internal.uProtobufVarint;

class function TProtobufTag.WithData(aFieldNumber: TProtobufFieldNumber; aWireType: TProtobufWireType): TProtobufTag;
begin
  result.FFieldNumber := aFieldNumber;
  result.FWireType := aWireType;
end;

procedure TProtobufTag.Encode(aDest: TStream);
begin
  EncodeVarint((FieldNumber shl 3) or Ord(WireType), aDest);
end;

procedure TProtobufTag.Decode(aSource: TStream);
var
  lVarint: UInt64;
begin
  lVarint := DecodeVarint(aSource);
  FFieldNumber := lVarint shr 3;
  FWireType := TProtobufWireType(lVarint and $7);
end;

end.
